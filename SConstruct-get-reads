import os
from os import path
import sys

# Ensure that a virtualenv is active before importing non-stdlib dependencies.
venv = os.environ.get('VIRTUAL_ENV')
if not venv:
    sys.exit('--> an active virtualenv is required'.format(venv))

from SCons.Script import (Environment, Variables, Help, Decider)

# check timestamps before calculating md5 checksums
Decider('MD5-timestamp')

# Define some PATH elements explicitly.
PATH=':'.join([
    'bin', path.join(venv, 'bin'),
    '/usr/local/bin', '/usr/bin', '/bin']
)

vars = Variables()
vars.Add('out', '', 'dada2')
vars.Add('nproc', 'Number of concurrent processes', default=12)

# Provides access to options prior to instantiation of env object
# below; it's better to access variables through the env object.
varargs = dict({opt.key: opt.default for opt in vars.options}, **vars.args)
truevals = {True, 'yes', 'y', 'True', 'true', 't'}

# define boolean variables like
# varname = varargs['varname'] in truevals

# SHELLOPTS sets shell options to fail (including piped commands) with
# nonzero exit status; this requires bash.
env = Environment(
    ENV=dict(os.environ, PATH=PATH),
    variables=vars,
    SHELL='bash'
)

Help(vars.GenerateHelpText(env))

# ############### start inputs ################

miseq_data = '/fh/fast/fredricks_d/bvdiversity/data'
plate12_info = path.join(miseq_data, 'miseq-plate-12-ireads/dada2/sample_info.csv')
plate30_info = path.join(miseq_data, 'miseq-plate-30/dada2/sample_info.csv')

dada2_img = (
    '/app/singularity/2.3.1.1/bin/singularity exec '
    '--bind $(pwd):$(pwd) '
    '--workdir $(pwd) '
    '/fh/fast/fredricks_d/bvdiversity/singularity/dada2-1.4.1b.img '
).replace('$', '$$')

# ############### end inputs ##################

env['dada2_img'] = dada2_img

sample_info_files = env.Command(
    target='$out/sample_info_list.txt',
    source=[plate12_info, plate30_info],
    action='ls $SOURCES > $TARGET'
)

sample_info = env.Command(
    target='$out/sample_info.csv',
    source=sample_info_files,
    action=('csvstack --groups $$(echo -n $$(cat $SOURCE) | tr " " ",") '
            '--group-name path '
            '$$(cat $SOURCE) > $TARGET')
)

sv_fa, sv_table, weight, specimen_map, sv_table_long = env.Command(
    target=['$out/seqs.fasta',
            '$out/dada2_sv_table.csv',
            '$out/weights.csv',
            '$out/specimen_map.csv',
            '$out/dada2_sv_table_long.csv'],
    source=sample_info,
    action=('$dada2_img bin/dada2_write_seqs.R $SOURCE '
            '--seqs ${TARGETS[0]} '
            '--sv-table ${TARGETS[1]} '
            '--weights ${TARGETS[2]} '
            '--specimen-map ${TARGETS[3]} '
            '--sv-table-long ${TARGETS[4]} ')
)
Depends(sv_fa, 'bin/dada2_write_seqs.R')

# create multiple alignment and identify bacterial sequences using
# cmalign alignment scores
seqs_sto, seqs_scores = env.Command(
    target=['$out/seqs.sto', '$out/sv_aln_scores.txt'],
    source=['data/ssu-align-0.1.1-bacteria-0p1.cm', sv_fa],
    action=('cmalign '
            '--cpu $nproc '
            '-o ${TARGETS[0]} '
            '--sfile ${TARGETS[1]} '
            '--noprob '
            '--dnaout '
            '$SOURCES > /dev/null')
)

# names of sequences with low bit scores
not_16s = env.Command(
    target='$out/not_16s.txt',
    source=seqs_scores,
    action='read_cmscores.py $SOURCE --min-bit-score 0 -o $TARGET'
)
